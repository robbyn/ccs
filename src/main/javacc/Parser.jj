options {
    STATIC = false;
    IGNORE_CASE = false;
}
PARSER_BEGIN(Parser)
package ccs.parser;
import ccs.compiler.Compiler;
import org.tastefuljava.classfile.CodeSegment;
import ccs.compiler.VariableDeclaration;
import ccs.compiler.Type;
import ccs.compiler.Strings;

public class Parser extends Compiler {
    public void parse() throws ParseException {
        input();
    }
}
PARSER_END(Parser)

SKIP :
{
< " " | "\t" | "\r" | "\n" | "\r\n" >
| <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n") >
| <"/*"(~["/"])*  "*""/" >
}
TOKEN : { < VAR: "VAR" > }
TOKEN : { < AS: "AS" >}
TOKEN : { < START: "START" > | < STOP: "STOP" > | < OUTPUT: "OUTPUT" > } 
TOKEN : /* KEYWORDS */
{
    <INT: "INT"> | <BOOL:"BOOL"> | <FLOAT:"FLOAT"> | <CHAR:"CHAR">
    |<AND: "AND"> | <OR: "OR"> | <NOT: "NOT">
}

TOKEN :
{
  < EQUALS: "=" > | < COLON: ":" > | <LPAR: "(">|<RPAR: ")">
}
TOKEN:
{
  < PLUS : "+" >|< MINUS :"-" >|< MUL: "*" >|< DIV: "/" >|< MOD: "%" >|< COMM: ",">
  |<LT: "<">|<GT: ">">|<LE: "<=">|<GE: ">=">|<EQ: "==">|<NE: "<>">
  |<CAT: "&">
}
TOKEN:
{
    < VARIABLE: <LETTER>(<LETTER>|<DIGIT>)* >
    | < #LETTER: ["a"-"z", "A"-"Z", "_"] >
    | < #DIGIT: ["0"-"9"] >
}
TOKEN:
{
    < INT_VALUE: (<DIGIT>)+ >
    | < FLOAT_VALUE: <INT_VALUE> "." (<DIGIT>)* | "." (<DIGIT>)+ >
    | < CHAR_VALUE: "\'" (~["\'","\\"]|<ESCAPE>) "\'" >
    | < STRING_VALUE: "\"" (~["\"","\\"]|<ESCAPE>)* "\"" >
    | < #ESCAPE: "[" ~[] "]" >
}

void input(): {} { (declaration())*  body() }
void declaration(): {}
{
    <VAR> varDeclaration(null) (<COMM> varDeclaration(null))* <AS> type()
}
VariableDeclaration varDeclaration(VariableDeclaration next):
    { Token n; CodeSegment seg = null; Type t = null; }
{
    ( n=<VARIABLE> (<EQUALS> { openSegment(); } t=literalValue() { seg = closeSegment(); })? )
    { return new VariableDeclaration(n.image,null,t,next); }
}
Type literalValue(): { Type result; }
{
    (
        <INT_VALUE> { result = Type.INT; }
        | <FLOAT_VALUE> { result = Type.FLOAT; }
        | <CHAR_VALUE> { result = Type.CHAR; }
        | <STRING_VALUE> { result = Type.STRING; }
    )
    { return result; }
}
Type type(): {Type result;} {
    (
        <INT> { result = Type.INT; }
        | <FLOAT> { result = Type.FLOAT; }
        | <BOOL> { result = Type.BOOL; }
        | <CHAR> { result = Type.CHAR; }
    )
    { return result; }
}
void body(): {} { <START> (statement())* <STOP> }
void statement(): {} { assignment() | output() }
void assignment(): {} { <VARIABLE> <EQUALS> expression() }
void output(): {} { <OUTPUT> <COLON> expression() }
void expression(): {} { comparison() (logicalOp() comparison())* }
void logicalOp(): {} { <AND> | <OR> }
void comparison(): {Token op; } {
    (
        simpleExpression()
        ( op=comparisonOp() simpleExpression() )?
    )
}
Token comparisonOp(): {Token t;} {
    ( t=<LT> | t=<GT> | t=<LE> | t=<GE> | t=<EQ> | t=<NE> )
    {return t;}
}
void simpleExpression(): {} { term() ( addOp() term() )* }
void addOp(): {} { <PLUS> | <MINUS> | <CAT> }
void term(): {} { factor(false) ( mulOp() factor(false) )* }
void mulOp(): {} { <MUL> | <DIV> | <MOD> }
void factor(boolean neg): {} {
    (
        (<PLUS>|<MINUS> {neg=!neg;}) factor(neg) {}
        | <LPAR> expression() <RPAR>
        | <NOT> expression()
        | <VARIABLE>
        | literalValue()
    )
}
